; Rutines per posar/copiar Grafics de tiles
;  on els dos primers bytes indiquen la mida que tenen
;
; PUTGFX : Posa Grafic a RAM BUFFER DE VIDEO
; ///////////////////////////////////////////////
; ///////// PUTGFX with TILE TRANSPARENT ////////
; ///////////////////////////////////////////////
; Posa un GFX de tiles en una posicio determinada del BUFFER a RAM
; IX = 	direccio RAM del GFX [2 BYTES ALÃ‡ADA i AMPLE]
; IY = 	direccion RAM del BUFFER desti
; C = ID de tile tranparent. No es pintara

; Cal RAM : res
; Toca	  : A,BC,DE,HL,IX,IY
; Retorna : res
; Usa fun.: res
PUTGFX_TRANS:
	; Carrego quantes lineas
	ld	A,(IX)
	ld	B,A	; Preparo contador
	inc	IX
	; Carrego quantes tiles d'ample
	ld	A,(IX)
	ld	E,A
	inc	IX
.LOOPFILA:
	push	BC
	ld	B,E	; Numero de tiles
.LOOPTILE:
	ld	A,(IX)	; Copio una tile
	cp	C
	jr	z,.NOPINTA
	ld	(IY),A
.NOPINTA:
	inc	IX
	inc	IY
	djnz	.LOOPTILE
	ld	A,32
	sub	E	; 32-Numero de tiles es el incremento de IY
	ld	C,A
	add	IY,BC
	pop	BC
	djnz	.LOOPFILA

	ret

