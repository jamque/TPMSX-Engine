; ////////////////////////////////
; //////// GETJOYMEGA3 ///////////
; ////////////////////////////////

; Agafa els valors del joystick especificat a A

; A = 	0 Joystick 1
;	1 Joystick 2
; HL =	Posicio variable joystick

; Cal RAM : res
; Toca	  : C,DE
; Retorna : Posicio (HL) bits 7-0 [START,A,C,B,Rt,Lt,Dn,Up] per 1, copia a A
; Usa fun.: BIOS -> WRTPSG, RDPSG

GETJOYMEGA3:
	or	A		; Equivale a CP 0 por flags
	jr	z,.GETJO1	;Select
	ld	DE,20CFh ; Port 2
	jr	.GETJ02
.GETJO1:
	ld	DE,108Fh ; Port 1
.GETJ02:
	ld	A,15
	call	WRTPSG
	m_RDJOYPORT
	and	3Fh
	ld	C,A	; c=0,0,C,B,Rt,Lt,Dn,Up
	m_FLIPPORTOUT
	m_RDJOYPORT
	and	3Ch
	add	A,A
	add	A,A
	ld	B,A	; b=ST,A,ID1,ID0,0000
	m_FLIPPORTOUT
	ld	A,C	; a=0,0,C,B,Rt,Lt,Dn,Up
	or	B	; a=ST,A,C,B,Rt,Lt,Dn,Up
	cpl
	ld	(HL),A
	ret
