; ///////////////////////////////////
; ///////// IWBVRMRLE ///////////////
; ///////////////////////////////////

; Write Bloq Video RaM en compresió RLE propia
; volca bytes desde direccio RAM HL a la direccio VRAM DE
; No utilitza BIOS, CAL deshabilitar les interupcions
; 29/12/04 Arnau Castellvi / Zona Neutra
; 2012 Rafel Arreglos RLE

; HL = 	direccio RAM [2 primers BYTES = TOTAL BYTES COMPRIMITS]
; DE = 	direccion VRAM

; Cal RAM : res
; Toca	  : A,BC,DE,HL,IX
; Retorna : res
; Usa fun.: res

IWBVRMRLE:
	ld 	A,E
	out 	(99h),A
	ld 	A,D
	or 	40h
	out 	(99h),A
	ld 	C,98h
	
	ld	A,(HL)	; Agafo quans bytes s'han de fer
	ld	E,A
	inc	HL
	ld	A,(HL)
	ld	D,A
	inc	HL
	
	push	DE
	pop	IX	; LD	IX,DE
.NEXT:
	ld	A,(HL)
	ld	B,A
	inc	HL
.LOOP:
	outi
	dec	HL
	inc	B
	djnz	.LOOP
	push	HL	; Guardo per quin byte anem [despres del color]
	push	IX
	pop	HL	; LD 	HL,IX
	dec	HL
	dec	HL
	ld	A,H
	or	A	; Equivale a CP 0 por flags
	jr	nz,.CONT
	ld	A,L
	or	A	; Equivale a CP 0 por flags
	jr	z,.FI
.CONT:
	push	HL
	pop	IX	;	LD IX,HL
	pop	HL	; Recupero per quin byte anem
	inc	HL
	jr	.NEXT
.FI:
	pop	HL	; Recupero per quin byte anem [per si surtu per FI]
	ret
